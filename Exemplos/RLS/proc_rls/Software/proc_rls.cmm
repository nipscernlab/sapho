#PRNAME proc_rls
#NUBITS 32
#NBMANT 23
#NBEXPO 8
#NDSTAC 5
#SDEPTH 5
#NUIOIN 2
#NUIOOU 2

float x[4];
float w[4];
float P[4][4];

void rls_update(float d)
{
    // calculo do erro e ------------------------------------------------------

    float e = d - <w|x>;
    
    // calculo do vetor de ganho K --------------------------------------------

    float Px[4] # |P|x>;
    float g     = 1.0/(0.99 + <x|Px>);
    float K [4] # g|Px>;

    // atualiza os pesos w ----------------------------------------------------

    w # |w> + e|K>;

    // atualiza a matriz P ----------------------------------------------------

    P # |P| - |K><Px|;
    P # 1.010101|P|; // (1/0.99)
}

void main()
{
    // inicializa P e w -------------------------------------------------------

    P # 1000.0|I|;
    w # |0>;

    // loop nos dados ---------------------------------------------------------

    int j = 0;
    while (j < 20)
    {
        // le os valores de x[n] ----------------------------------------------
        
        x # 0.001|in(0)>;

        // aplica uma iteracao RLS --------------------------------------------

        rls_update(in(1)*0.001);

        // externa os coeficientes w ------------------------------------------

        out(0,1000.0|w>);

        j++;
    }
}